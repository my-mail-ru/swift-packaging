#! /usr/bin/perl

use strict;
use warnings;
use JSON::XS;
use IPC::Open2;

my $pid = open2(my $out, my $in, '/usr/lib/rpm/find-requires');

my $has_modules;
while (<STDIN>) {
	print $in $_;
	$has_modules = 1 if /\.swiftmodule$/;
}
close $in;

my $reqpkgs = '';
while (<$out>) {
	print;
	chomp;
	$reqpkgs .= `rpm -q --whatprovides '$_' --queryformat '%{NAME} %{VERSION}\n'`;
}
close $out;

waitpid($pid, 0);

my %requires;
$requires{$1} = $2 while $reqpkgs =~ /^(swift[\w-]*) ([\d\.]+)$/mg;
print "$_ >= $requires{$_}\n" foreach sort keys %requires;

if ($has_modules) {
	my ($builddir) = @ARGV;
	my $tools_version = `cd $builddir && swift package tools-version`;
	my $json = `cd $builddir && swift package dump-package`;
	$json =~ s/^.*?(?=\{)//s;
	my $data = decode_json($json);
	foreach my $dep (@{$data->{dependencies}}) {
		if (my $exact = $dep->{requirement}->{exact}) {
			print "swiftpm($dep->{url}) = $exact->[0]\n";
		} elsif (my $range = $dep->{requirement}->{range}) {
			print "swiftpm($dep->{url}) >= $range->[0]->{lowerBound}\n";
		}
	}
}
